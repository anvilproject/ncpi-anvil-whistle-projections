def ConditionAssertion(config, study_meta, condition) {
    // currently dropping rows that don't have codes, as per Robert 4/7/25, but allows plain codeableConcept.text
    if (condition.code?){
    if (~condition.value_number and ~condition.value_units_display and ~condition.value_units?){
    meta.tag[]: StudyMeta(config, study_meta);
    //meta.profile[]: "https://nih-ncpi.github.io/ncpi-fhir-ig-2/StructureDefinition/ncpi-condition";
    resourceType: "Observation";
    status: "final";

    if (condition.code?) {
        var condition_code: condition.code;
    } else {
        var condition_code: condition.display;
        }

    identifier[]: BuildConditionIdentifier(config, condition_code, condition.subject_id, condition);
    identifier[0].use: "official";
    subject: ParticipantsCondition(condition);
    code: CompletePartialCoding(condition.code, condition.display, "curie_system");    
    _effectiveDateTime.extension[0] (if condition.age_at_assertion?): AgeAtExtension(config, condition.subject_id, condition.age_at_assertion, "years");
    valueCodeableConcept.coding[]: HarmonizeMapped(condition.value_code, "condition_assertion");
    valueCodeableConcept.text: condition.display;
    category[].coding[] (if condition.assertion_type?): HarmonizeMappedFirst(condition.assertion_type, "assertion_type");

    component[] (if condition.age_at_event?): BuildAgeAtOnset(config, condition);
    component[] (if condition.age_at_resolution?): BuildAgeAtResolution(config, condition);
    }
    }
    }
    
    

def BuildAgeAtResolution(config, condition) {
    code: {
        coding[]: {
            code: "age_at_resolution";
            system: "https://anvilproject.org/acr-harmonized-data-model/";
            display: "Age at resolution";
            }
            }
    _valueDateTime.extension[]: AgeAtExtension(config, condition.subject_id, condition.age_at_resolution, "years")
}

def BuildAgeAtOnset(config, condition) {
    code: {
        coding[]: {
            code: "age_at_onset";
            system: "https://anvilproject.org/acr-harmonized-data-model/";
            display: "Age at onset";
            }
            }
    _valueDateTime.extension[]: AgeAtExtension(config, condition.subject_id, condition.age_at_event, "years")
}

def GetAssertionValue(assertion) {
    var value (if assertion.value_display?): assertion.value_display;
    var value (if assertion.value_number?): assertion.value_number;
    var value (if assertion.value_code?): assertion.value_code;
    $this: value;
}

def BuildConditionIdentifier(config, required condition_code, required subject_id, condition) {
    var value: GetAssertionValue(condition);
    $this: Key_Identifier(config, "Condition", $StrCat(subject_id, "|", condition_code, "|", value));
}

def ParticipantsCondition(condition) {
      reference: $StrCat("Patient","/",condition.subject_id)
}

// Currently supporting both "MIM" and "OMIM" curies for the OMIM ontology.
def BuildConditionCode(condition) {
    if (condition.code?) {
        var SplitCode: $StrSplit(condition.code, ":");
    coding[]: {
        system: HarmonizeAsSystem(SplitCode[0], "curie_system");
        code: condition.code;
        display: condition.display;
    }
    text: condition.display;
    }
}

def CompletePartialCoding(resource_code, resource_display, resource_system) {
    if (resource_code?) {
        var SplitCode: $StrSplit(resource_code, ":");
        coding[]: {
            system: HarmonizeAsSystem(SplitCode[0], resource_system);
            code: resource_code;
            display: resource_display;
        }
    }  
    text: resource_display;
}

def SubjectAssertion(config, study_meta, assertion){
    if (assertion.value_number or assertion.value_units_display or assertion.value_units?) {
     meta.tag[]: StudyMeta(config, study_meta);
    //meta.profile[]: "https://nih-ncpi.github.io/ncpi-fhir-ig-2/StructureDefinition/ncpi-condition";
    resourceType: "Observation";
    status: "final";

    if (assertion.code?) {
        var assertion_code: assertion.code;
    } else {var assertion_code: assertion.display;}

    identifier[]: BuildConditionIdentifier(config, assertion_code, assertion.subject_id, assertion);
    identifier[0].use: "official";
    subject: ParticipantsCondition(assertion);
    code: CompletePartialCoding(assertion.code, assertion.display, "curie_system");    
    _effectiveDateTime.extension[0] (if assertion.age_at_assertion?): AgeAtExtension(config, assertion.subject_id, assertion.age_at_assertion, "years");
    category[].coding[] (if assertion.assertion_type?): HarmonizeMappedFirst(assertion.assertion_type, "assertion_type");
    valueQuantity: {
        value (if assertion.value_number?): $ParseFloat(assertion.value_number);
        unit (if assertion.value_units_display?): assertion.value_units_display;
        code (if assertion.value_units?): assertion.value_units;
        system: "http://unitsofmeasure.org";
        };
    }
}


def ProcessCondition(config, study_meta, participant) {
    var conditions: $Unique(participant.condition);

    out condition: ConditionAssertion(config, study_meta, conditions[]);
    out assertion: SubjectAssertion(config, study_meta, conditions[]);
} 