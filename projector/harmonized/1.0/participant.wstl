def AgeAtLastVitalStatus(age) {
    if (age?) {
    url: "https://nih-ncpi.github.io/ncpi-fhir-ig-2/StructureDefinition/research-age-at-last-vital-status";
    valueQuantity.value: $ParseInt(age);
    }
}

def DobMethod(method) {
    if (method?) {
        var MethodValueCoding: HarmonizeMappedFirst(method, "Date of Birth Type");

        if (MethodValueCoding?) {
            url: "https://nih-ncpi.github.io/ncpi-fhir-ig-2/StructureDefinition/research-date-of-birth-method";
            valueCoding : MethodValueCoding;      
        } 
}
}

def Participant(config, study_meta, participant) {
    meta.tag[]: StudyMeta(config, study_meta);
    //meta.profile[]: "https://nih-ncpi.github.io/ncpi-fhir-ig-2/StructureDefinition/ncpi-participant";
    id: participant.subject_id;
    resourceType: "Patient";
    identifier[]: Key_Identifier(config, "Patient", participant.subject_id);
    identifier[0].use: "official";

    var races: $StrSplit(participant.demographics[0].race, "|");
    extension[]: RaceExtension(races[], participant.demographics[0].race_display);
    extension[]: EthnicityExtension(participant.demographics[0].ethnicity, participant.demographics[0].ethnicity_display);
    extension[]: SexExtension(participant.demographics[0].sex);
    extension[]: AgeAtLastVitalStatus(participant.demographics[0].age_at_last_vital_status);
    deceasedBoolean (if participant.demographics[0].vital_status = "Dead"): true;
    birthDate: participant.demographics[0].date_of_birth;
    extension[]: DobMethod(participant.demographics[0].date_of_birth_type);
}

def ProcessParticipants(config, study_meta, participants) {
    out participant: Participant(config, study_meta, participants);
}